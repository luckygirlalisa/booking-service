apply plugin: "jacoco"

String[] exclusionPatterns = [
        "order/OrderApplication.kt",
        "**/dto/**",
        "**/vo/**",
        "**/exception/**",
        "**/model/**",
        "**GlobalExceptionHandler**"
]

project.ext.set('coverageExclusionPatterns', exclusionPatterns)

jacoco {
    toolVersion = '0.8.7'
}

jacocoTestReport {
    shouldRunAfter test
    reports {
        xml.enabled false
        csv.enabled false
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: exclusionPatterns)
        })
    }
}

jacocoTestCoverageVerification {
    shouldRunAfter test
    executionData test
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'INSTRUCTION'
                minimum = 0.85
            }
        }

        rule {
            element = 'BUNDLE'
            limit {
                counter = 'BRANCH'
                minimum = 0.85
            }
        }
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: exclusionPatterns)
        })
    }
}

test {
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestReport
check.dependsOn jacocoTestCoverageVerification
